plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '[7.0.52,8.0)'
    id 'org.cadixdev.licenser' version '0.6.1'
}

apply from: "properties.gradle"

version = modVersion
group = modGroup
archivesBaseName = modBaseName
ext.versionTag = ''

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

license {
    header = project.file('LICENSE-HEADER')
    include '**/*.java'
}

dependencies {
    implementation "net.neoforged:neoforge:${forgeVersion}"
}

minecraft {
    mappings {
        version(mappingsVersion)
    }
}

if (file('private.gradle').exists()) {
    apply from: 'private.gradle'
}

def registerModule(LinkedHashMap<String, String> moduleData) {
    def defaultManifestAttributes = ["Specification-Title"     : "Compound",
                                     "Specification-Vendor"    : "Trident",
                                     "Implementation-Title"    : project.name,
                                     "Implementation-Version"  : "${version}",
                                     "Implementation-Vendor"   : "Trident",
                                     "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                                     "FMLModType"              : "GAMELIBRARY"]

    def binaryJar = tasks.register(moduleData.taskName, Jar) {
        archiveBaseName = moduleData.artifactName
        from(sourceSets.main.output) {
            include moduleData.sources
        }
        manifest {
            attributes(defaultManifestAttributes)
        }
    }
    def sourcesJar = tasks.register(moduleData.taskName + "Sources", Jar) {
        archiveBaseName = moduleData.artifactName
        archiveClassifier = 'sources'
        from(sourceSets.main.allSource) {
            include moduleData.sources
        }
        manifest {
            attributes(defaultManifestAttributes)
        }
    }

    artifacts.add("archives", binaryJar)
    artifacts.add("archives", sourcesJar)

    publishing.publications.create(moduleData.publicationName, MavenPublication) {
        groupId 'com.tridevmc.compound'
        artifactId moduleData.artifactName
        version project.version + versionTag
        artifact binaryJar
        artifact sourcesJar
    }
}

apply from: "modules.gradle"

compoundModules.each { LinkedHashMap<String, String> moduleData ->
    registerModule(moduleData)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publish.dependsOn('reobfJar')